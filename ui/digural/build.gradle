/*
 * Copyright 2017-2024 Cyface GmbH
 *
 * This file is part of the Cyface App for Android.
 *
 * The Cyface App for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface App for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface App for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Gradle's build file for the app.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 */
apply plugin: 'com.android.application'
apply plugin: 'io.sentry.android.gradle' // Exception tracking when using proguard
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin' // recommended to navigate between fragments
apply plugin: 'kotlin-parcelize' // Generate Parcelable in Kotlin from Annotation
apply plugin: 'com.google.protobuf' // For Proto DataStore

buildscript {
    repositories {
        mavenCentral()
        google() // For androidx.navigation
    }

    dependencies {
        // Exception tracking when using proguard
        classpath "io.sentry:sentry-android-gradle-plugin:$rootProject.ext.sentryAndroidGradlePluginVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$rootProject.ext.androidNavigationPluginVersion"
    }
}

android {
    namespace 'de.cyface.app.digural'
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId = "de.cyface.app.digural"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        // If our terms change that much that they to be re-accepted, increase this (see Confluence!)
        buildConfigField "int", "currentTerms", "5"

        // Placeholders for AndroidManifest.xml
        manifestPlaceholders = [
                // Load Google Maps API key
                googleMapsApiKey:"${project.findProperty('google.maps_api_key')}",

                // The SDK requires us to replace this, but we don't use it
                'appAuthRedirectScheme': 'NOT_IN_USE'
        ]
    }

    buildTypes {
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true

            // Staging
            buildConfigField "String", "collectorApi", "\"${project.findProperty('digural.staging_api')}\""
            manifestPlaceholders = [usesCleartextTraffic:"true"] // external Digural camera API uses http
        }
        release {
            // mapping.xml file required to decode stack traces, but it's included in the bundle
            minifyEnabled true
            // https://developer.android.com/studio/build/shrink-code.html
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // signingConfig is set by the CI
            buildConfigField "String", "collectorApi", "\"${project.findProperty('digural.api')}\""
            manifestPlaceholders = [usesCleartextTraffic:"true"] // external Digural camera API uses http
        }
    }

    testOptions {
        unitTests {
            // Required so that logging methods do not throw not mocked exceptions in junit tests.
            returnDefaultValues = true
        }
    }

    lint {
        abortOnError false
    }

    // Enabling desugaring to support Java 8 and Java 11 features
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility

        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinTargetJavaVersion
    }

    // Avoid `2 files found with path ...` error
    packagingOptions {
        resources {
            // To resolve the conflict warning after adding google-api-client dependency
            excludes += ['META-INF/DEPENDENCIES']
            excludes += ['META-INF/INDEX.LIST']
        }
    }

    buildFeatures {
        viewBinding true
    }
}

// Exception tracking
sentry {
    // Disables or enables the handling of Proguard mapping for Sentry.
    // If enabled the plugin will generate a UUID and will take care of
    // uploading the mapping to Sentry. If disabled, all the logic
    // related to proguard mapping will be excluded.
    // Default is enabled.
    includeProguardMapping = true

    // Whether the plugin should attempt to auto-upload the mapping file to Sentry or not.
    // If disabled the plugin will run a dry-run and just generate a UUID.
    // The mapping file has to be uploaded manually via sentry-cli in this case.
    // Default is enabled.
    autoUploadProguardMapping = true

    // Disables or enables the automatic configuration of Native Symbols
    // for Sentry. This executes sentry-cli automatically so
    // you don't need to do it manually.
    // Default is disabled.
    uploadNativeSymbols = false

    // Does or doesn't include the source code of native code for Sentry.
    // This executes sentry-cli with the --include-sources param. automatically so
    // you don't need to do it manually.
    // Default is disabled.
    includeNativeSources = false
}

dependencies {
    // Exception tracking
    implementation "io.sentry:sentry-android:$rootProject.ext.sentryAndroidVersion"

    // Proto DataStore with MultiProcess support to store settings
    implementation "androidx.datastore:datastore:${datastoreVersion}"
    implementation  "com.google.protobuf:protobuf-javalite:${protobufVersion}"

    // To change the material design
    implementation "androidx.appcompat:appcompat:$rootProject.ext.androidxAppCompatVersion"
    // To use material elements (tabs, nav bar, slider, etc.)
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"
    // Progress bar used to display upload progress
    implementation "com.github.lzyzsd:circleprogress:$rootProject.ext.circelProgressVersion"
    // To use Google Map
    implementation "com.google.android.gms:play-services-maps:$rootProject.ext.mapPlayServicesVersion"
    implementation "com.google.android.gms:play-services-location:$rootProject.ext.locationPlayServicesVersion"
    // To stream data changes via LiveData and ViewModel to the UI
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.ext.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.ext.lifecycleVersion"
    // To use the navigation graph
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.navigationVersion"
    // For the action bar (at the bottom of the screen)
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.navigationVersion"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$rootProject.ext.coreLibraryDesugaringVersion"

    // Kotlin components
    //api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.coroutinesVersion"
    //api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.coroutinesVersion"

    // Used for WebDAV Communication
    implementation "com.github.thegrizzlylabs:sardine-android:$rootProject.ext.sardineVersion"

    // Cyface dependencies
    implementation "de.cyface:android-utils:$rootProject.ext.cyfaceUtilsVersion"
    implementation project(':datacapturing')
    implementation project(':synchronization')
    implementation project(':persistence')
    implementation project(':energy_settings')
    implementation project(':camera_service')
    implementation project(':utils')
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    // Retrofit with Scalar Converter
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttpVersion" // Coroutines
    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android'


    // Dependencies for instrumentation tests
    // Resolve conflicts between main and test APK (which is used in the integration-test module):
    //androidTestImplementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"
    //androidTestImplementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    //androidTestImplementation "androidx.test:runner:$rootProject.ext.runnerVersion"
    //androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"
    //androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    // UiAutomator Testing
    //androidTestImplementation "androidx.test.uiautomator:uiautomator:$rootProject.ext.uiAutomatorVersion"
    //androidTestImplementation "org.hamcrest:hamcrest-integration:$rootProject.ext.hamcrestVersion"

    // Dependencies for local unit tests
    // - If Junit symbols are not resolvable in IntelliJ, make sure Build Variant is set to debug
    // - Loading another dependency (e.g. module) only it's production dependencies (compile) are loaded but not other dependencies (e.g. testCompile)
    testImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Optional - For better debuggable asserts
    //testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
}

// Required for Proto DataStore
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    // Generates the java Protobuf-lite code for the Protobuf files in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
